# Recreating Zork 285 in ZIL - Part 6 - The object table and how the player moves around
This is part 6 in an ongoing series. The previous part: "Part 5 - Parsing the input" is [here](https://github.com/heasm66/mdlzork/blob/master/zork_285/doc/zork285_05.md).  
  
In this part we will examine how the objects and their properties are stored in the Z-machine. We will also look at what available ZIL functions there are to manipulate the objects and properties. In the end we will see how this information is applied when we study how the player can use directions to move around the world model.
## The object table and the property tables
The object table and the property tables are described in [chapter 12 of The Z-Machine Standards Document](https://www.inform-fiction.org/zmachine/standards/z1point0/sect12.html). Much of the information in this part comes from there and it is essential reading for understanding the Z-machine.  
  
The starting point of the object table is stored in bytes 10-11 of the game header (the game header is described in chapter 11 of *The Z-Machine Standards Document*). This value is considered read-only but you can read it from inside ZIL by either the function ```<LOWCORE OBJECT>``` or simply the function ```<GET 0 5>``` (i.e. get the fifth 2-byte word). Normally this is a value you won't need.  
The object table is divided into three parts following each other in the order; the property default table, the object tree table and the property tables.  

There are differences between version 3 and the later versions that will affect the size of these tables. These differences are:
* Version 3 only allows up to 32 attribute flags; later versions can have up to 48.
* Version 3 only allows up to 255 objects; later versions can address up to 65535 (one byte versus two bytes) but because the object table is in dynamic memory and must reside under address $FFFF and the minimum size of an object is 16 bytes, the in practice the upper limit of objects is well under 4096.
* Version 3 only allows up to 31 properties; later versions can have up to 63.
In the examples below we will primarily focus later versions but point out the differences and leave the version 3 format as an exercise to the reader.  
  
The simple ZIL program below creates a couple of objects and properties and will be used as an example henceforth.
```
<VERSION XZIP>          ;"Version 5"
<CONSTANT RELEASEID 1>

<DIRECTIONS NORTH EAST WEST SOUTH UP DOWN>

<GLOBAL MAGIC-FLAG <>>

<ROUTINE WINDOW-EXIT () <RTRUE>>
<ROUTINE LIVING-ROOM-F () <RTRUE>>

;"If defined, this CONSTANT is assigned the highest object number during compilation."
<CONSTANT LAST-OBJECT <>> 

<OBJECT ROOMS>

<ROOM LIVING-ROOM  ;"ROOM and OBJECT are synonyms for same function."
      (IN ROOMS)
      (DESC "Living Room")
      (LDESC "You are in the living room. There is a doorway to the east.")
      (NORTH "You can't enter the chimney!")
      (EAST TO KITCHEN)
      (WEST TO STRANGE-PASSAGE IF MAGIC-FLAG ELSE "The door is nailed shut.")
      (DOWN TO CELLAR IF TRAP-DOOR IS OPEN)
      (SOUTH PER WINDOW-EXIT)
      (ACTION LIVING-ROOM-F)
      (FLAGS LANDBIT LIGHTBIT)>  

<ROOM KITCHEN
      (IN ROOMS)
      (DESC "Kitchen")
      (WEST TO LIVING-ROOM)>

<ROOM STRANGE-PASSAGE
      (IN ROOMS)
      (DESC "Strange Passage")>

<ROOM CELLAR
      (IN ROOMS)
      (DESC "Cellar")>
      
<OBJECT LANTERN
    (DESC "brass lantern")
    (IN LIVING-ROOM)
    (SYNONYM de)
    (ADJECTIVE BRASS)>

<OBJECT SWORD
    (IN LIVING-ROOM)
    (DESC "sword")
    (SYNONYM SWORD)>

<OBJECT TRAP-DOOR
    (DESC "trap-door")
    (IN LIVING-ROOM)
    (SYNONYM TRAP-DOOR DOOR TRAPDOOR)
    (FLAGS OPENBIT)>
    
<ROUTINE GO () 
    <CRLF>
    <TELL "<LOWCORE OBJECT>=" N <LOWCORE OBJECT> CR>
    <TELL "P?NORTH=" N ,P?NORTH CR>
    <TELL "P?EAST=" N ,P?EAST CR>   
    <TELL "P?WEST=" N ,P?WEST CR>   
    <TELL "P?SOUTH=" N ,P?SOUTH CR> 
    <TELL "P?UP=" N ,P?UP CR>   
    <TELL "P?DOWN=" N ,P?DOWN CR>   
    <TELL "P?SYNONYM=" N ,P?SYNONYM CR> 
    <TELL "P?ADJECTIVE=" N ,P?ADJECTIVE CR> 
    <TELL "P?LDESC=" N ,P?LDESC CR> 
    <TELL "P?ACTION=" N ,P?ACTION CR>   
    <TELL "ROOMS=" N ,ROOMS CR> 
    <TELL "LIVING-ROOM=" N ,LIVING-ROOM CR> 
    <TELL "KITCHEN=" N ,KITCHEN CR> 
    <TELL "STRANGE-PASSAGE=" N ,STRANGE-PASSAGE CR> 
    <TELL "CELLAR=" N ,CELLAR CR>   
    <TELL "LANTERN=" N ,LANTERN CR> 
    <TELL "SWORD=" N ,SWORD CR> 
    <TELL "TRAP-DOOR=" N ,TRAP-DOOR CR> 
    <TELL "LAST-OBJECT=" N ,LAST-OBJECT CR>>
```
If we compile and run the program it produces a listing of constants that will be usefull. The compilation also created four abbreviations in this order; "P?", " the ", "door" and "You ". 
```
<LOWCORE OBJECT>=282
P?NORTH=63
P?EAST=62
P?WEST=61
P?SOUTH=60
P?UP=59
P?DOWN=58
P?SYNONYM=57
P?ADJECTIVE=56
P?LDESC=55
P?ACTION=54
ROOMS=8
LIVING-ROOM=7
KITCHEN=6
STRANGE-PASSAGE=5
CELLAR=4
LANTERN=3
SWORD=2
TRAP-DOOR=1
LAST-OBJECT=8
```

### The property default table
The property default table starts at the address from the game header and holds the 31 (version 3) or 63 (version 4 and later) default values for each property, occupying 62 bytes or 126 bytes, respectively. These are the values returned when reading a property on an object that don't have that property explicitly defined. The default values are initialized to 0 (false) but can be set to other values by ```PROPDEF```. ```<PROPDEF SIZE 5>``` will, for example, give all objects SIZE=5 if SIZE is not explicitly defined to another value for that object.  
  
In our example game the property default table starts at address 282, hexadecimal $011A, and contains 126 bytes of 0, meaning that all 63 properties have a default value of 0.
```
011A-0197 00 00 00 00 ... 00 00
```
ZIL functions that operates on the property default table are; ```PROPDEF```.
### The object tree table
The object tree table follows directly after the property defaults table and consists of an entry of 9 bytes (version 3) or 14 bytes (version 4 or later) for each object. The first entry is object number 1 (object number 0 is considered "nothing" or "null").
  
The layout of the table as described in §12.3.1 and §12.3.2 of *The Z-Machine Standards Document*) is:
```
Version 3:
    the 32 attribute flags    parent     sibling     child   properties
   ---32 bits in 4 bytes---   ---3 bytes------------------  ---2 bytes--

Version 4 and later:
    the 48 attribute flags    parent     sibling     child   properties
   ---48 bits in 6 bytes---   ---3 words, i.e. 6 bytes----  ---2 bytes--   
```
In our example the object tree table starts at address $0198 ($011A + $8E) and look like this:
```
Byte 0-5                 6-7     8-9     10-11   12-13
     48 bit flags        Parent  Sibling Child   Properties
0198 00 00 00 00 00 01   00 07   00 02   00 00   02 08      Object 1 TRAP-DOOR, Bit 0, OPENBIT, is set
01A6 00 00 00 00 00 00   00 07   00 00   00 00   02 18      Object 2 SWORD
01B4 00 00 00 00 00 00   00 07   00 01   00 00   02 21      Object 3 LANTERN
01B2 00 00 00 00 00 00   00 08   00 05   00 00   02 36      Object 4 CELLAR
01D0 00 00 00 00 00 00   00 08   00 06   00 00   02 3E      Object 5 STRANGE-PASSAGE
01DE 00 00 00 00 00 00   00 08   00 00   00 00   02 4C      Object 6 KITCHEN
01EC 00 00 00 00 00 06   00 08   00 04   00 03   02 57      Object 7 LIVING-ROOM, Bit 1 and 2 are set (LIGHTBIT & LANDBIT)
01FA 00 00 00 00 00 00   00 00   00 00   00 07   02 86      Object 8 ROOMS
```
It is easy to identify ```ROOMS``` at the top in the object tree, it points to its first child, ```LIVING-ROOM```, that in its turn points to a sibling chain, ```LIVING-ROOM --> CELLAR --> STRANGE-PASSAGE --> KITCHEN```, and its first child, ```LANTERN```, and so on.  

  
Notice that flags don't need to be explicitly defined, instead the compiler assigns them a free slot during compilation. In the above table we can see that ```OPENBIT```, ```LIGHTBIT``` and ```LANDBIT``` are assigned bits 0-2, respectively.  
  
Also note that object numbers and attribute flags are assigned in reverse order; The last one specified in the source file get number 1, the next to last number 2 and so on. 
  
ZIL functions that operates on the object tree table are; ```PROPDEF```. ```FIRST?```, ```IN?```, ```LOC```, ```MAP-CONTENTS```, ```MOVE```, ```NEXT?```, ```PRINTD``` and ```REMOVE```.
### The property tables
The property tables immediately follow the object tree table and each object has its own property table. Each object in the object tree table have a pointer (byte 7-8 for version 3 and byte 12-13 for later versions) that point to the start address for that objects properties. Each property table starts with a header that holds the short description of the object (the ```DESC```-part). The layout of the header is described in §12.4 of *The Z-Machine Standards Document*:
```
   text-length     text of short name of object
  -----byte----   --some even number of bytes---
```
§12.4 also state "where the text-length is the number of 2-byte words making up the text, which is stored in the usual format. (This means that an object's short name is limited to 765 Z-characters.)". The limit of 765 z-chars isn't entirely correct because it is perfectly legal to use abbreviations and abbreviations don't have that limit, you could circumvent the limitation if you really want it.  
  
The actual properties follow after the header for this object in descending order of the property number. Property number 0 indicates the end of the table for this object. The layout of each property, from *The Z-Machine Standards Document*, §12.4.1 and §12.4.2, is:
```
Version 3:
   size byte     the actual property data
                ---between 1 and 8 bytes--

Version 4 and later: 
   size and number       the actual property data
  --1 or 2 bytes---     --between 1 and 64 bytes--
```
For version 3 the *size byte* have the property number in bits 0-4 (1-31) and the property size in bits 5-7 (add 1 to this value to get actual size of 1-8).  
  
Version 4 and later can have 1 or 2 bytes for size and number depending on the size of the property.  
If the size of the property is 1 or 2 bytes, only 1 byte is used for size and number. In this case bit 7 of the size and number byte is clear, bit 6 tells the property size (0=1 byte, 1=2 bytes) and bits 0-5 is the property number.  
If the size of the property is more than 2 bytes, 2 bytes is used for size and number. In this case bit 7 of the first byte is set; bit 6 of the first byte is unused but is always clear by convention and bits 0-5 of the first byte is the size of the property. In the second byte, bits 6-7 are unused but bit 7 is always set by convention and bit 6 is set in ZIL and clear in Inform, bits 0-5 holds the property number. A size of 0 is considered to mean that the size is 64 bytes.  
  
In our example the property tables looks like this:
```
0208 03 66 E6 54 BC 84 45               Length=5 2-byte words, "trap-door" 
     B9 86 02 FD 02 B5 03 06            89 86 --> Prop#=57, Size=6, W?TRAP-DOOR W?DOOR W?TRAPDOOR
     00                                 Null

0218 02 63 94 DD 25                     Length=2 2-byte words, "sword"
     79 02 F4                           79 --> Prop#=57, Size=2, W?SWORD
     00                                 Null

0221 05 1E E6 63 00 44 D3 65 57 CC A5   Length=5 2-byte words, "brass lantern"
     B9 84 02 D9 02 D0                  89 84 --> Prop#=57, Size=4, W?LANTERN W?LAMP
     78 02 AC                           78    --> Prop#=56, Size=2, W?BRASS
     00                                 Null

0236 03 11 0A 46 26 DC A5               Length=3 2-byte words, "Cellar"
     00                                 Null

023E 06 13 19 5C D3 31 40 12 A6 63 06 B1 45 Length=6 2-byte words, "Strange Passage"
     00                                     Null

024C 03 12 0E 65 0D AA 65               Length=3 2-byte words, "Kitchen"
     7D 00 07                           7D    --> Prop#=61, Size=2, UEXIT: goto room 7
     00                                 Null

0257 05 12 2E 6D D3 30 04 5E 94 C8 A5   Length=5 2-byte words, "Living Room"
     BF 83 01 3A 00                     BF 83 --> Prop#=63, Size=3, NEXIT: Message at 4x$013A
     7E 00 06                           7E    --> Prop#=62, Size=2, UEXIT: goto room 6
     BD 85 00 05 01 2B 10               BD 85 --> Prop#=61, Size=5, CEXIT: goto room 5 if variable 10,MAGIC-FLAG, is true else print message at 4x$012B
     BC 84 00 C9 00 00                  BC 84 --> Prop#=60, Size=4, FEXIT: goto room returned by routine at 4x$00C9
     BA 86 00 04 00 01 00 00            8A 86 --> Prop#=58, Size=6, DEXIT: goto room 4 if object 1 has bit 1, OPENBIT, set else print message at $0000 (no msg)
     77 01 2F                           77    --> Prop#=55, Size=2, LDESC: Message at 4x$012F
     76 00 CA                           76    --> Prop#=54, Size=2
     00                                 Null

0286 01 94 A5                           Length=1 2-byte words, ""
     00                                 Null
```
The first objects at $0208 have the header ```03 66 E6 54 BC 84 45```. The first byte is the length in 2-byte words of the following text. This 6 bytes in binary are and translates to the following z-characters; ```0 11001='t' 10111='r' 00110='a' 0 10101='p' 00101=shift to A2 11100='-' 1 00001 00010=use abbrev 2 ('door') 00101=padding```.  After the property header comes the first property, ```B9 86 02 FD 02 B5 03 06```. ```$B9 = 1 0 111001```, meaning bit 7 is set (size in next byte) and property number is 57 (bits 0-5). ```$86 = 1 0 000110``` means that the size is 6 bytes (bits 0-5). The ```02 FD 02 B5 03 06``` is pointers to ```W?TRAP-DOOR```, ```W?DOOR``` and ```W?TRAPDOOR``` in the vocabulary, respectively. ```$00 = property 0``` means that this is the end of this property table.  

The next object at $0218 is "sword" and its first property is ```79 02 F4```. The size and number byte is ```$79 = 0 1 111001```. Because bit 7 is clear the size is in bit 6 that is set, meaning a size of 2 bytes. The property number in bits 0-5 is 57. The property data is a pointer to the word ```W?SWORD``` in the vocabulary.  
### Directions in the property data
Drections are a bit of a special case and ZIL have a predefined default record size and pattern for the direction properties. This definition is redefinable if one would want to but the default format is as below (version 4 and later, version 3 have different values for the size).
```
<PROPDEF DIRECTIONS <>
  (DIR TO R:ROOM = (UEXIT 2) (REXIT <ROOM .R>))
  (DIR SORRY S:STRING = (NEXIT 3) (NEXITSTR <STRING .S>) <BYTE 0>)
  (DIR PER F:FCN = (FEXIT 4) (FEXITFCN <WORD .F>) <WORD 0>)
  (DIR TO R:ROOM IF G:GLOBAL "OPT" ELSE S:STRING = (CEXIT 5) (REXIT <ROOM .R>) (CEXITSTR <STRING .S>) (CEXITFLAG <GLOBAL .G>))
  (DIR TO R:ROOM IF D:OBJECT IS OPEN "OPT" ELSE S:STRING = (DEXIT 6) (REXIT <ROOM .R>) (DEXITOBJ <OBJECT .D>) (DEXITSTR <STRING .S>))
  (DIR R:ROOM = (UEXIT 2) (REXIT <ROOM .R>))
  (DIR S:STRING = (NEXIT 3) (NEXITSTR <STRING .S>) <BYTE 0>)>
```
The syntax of ```PROPDEF``` is explained in more detail in the [ZILF Reference Guide](https://github.com/heasm66/ZILF-Reference-Guide/raw/master/ZILF%20Reference%20Guide.pdf) but simply explained the ```DIR``` is a token placeholder for any direction type words (part of speech = direction) and the part after the ```=``` are the record size and then follows the record data. An expression like ```(UEXIT 2)``` both sets the record size to 2 and assigns ```UEXIT``` the value 2.  
As you probably already noticed the different exit types have different sizes. This is by design and the size is used to distinguish which type of exit it is.  
  
These are the five different type of exits that can be attached to a direction property and is stored in the object property tables as on object at $0257 above:
```
Name  Size 
UEXIT   2  "Unconditional exit" - goto the room stored in property position 0-1 
                                  Syntax: ([DIR] TO [ROOM]) or ([DIR] [ROOM])
NEXIT   3  "Non-exit"           - no exit but print message at packed address in property position 0-1 
                                  instead of standard message. Padded with 0 in position 2 to get to size=3.
                                  Syntax: ([DIR] "[message]") or ([DIR] SORRY "[message]")
FEXIT   4  "Function exit"      - goto the room returned by routine at packed address in property position 0-1.
                                  If function returns false, don't move. Padded with 0 in position 2-3 to get to size=4.
                                  Syntax: ([DIR] PER [ROUTINE-EXIT]) 
CEXIT   5  "Conditional exit"   - goto the room stored in property position 0-1 if value in variable in position 4 is true
                                  else print message in packed address in position 2-3. If position 2-3 is 0 then print
                                  the standard message.
                                  Syntax: ([DIR] TO [ROOM] IF [VARIABLE] [ELSE "[message]"])
DEXIT   6  "Door exit"          - goto the room stored in property position 0-1 if object with object number stored in 
                                  position 2-3 meets condition (check done in parser library) else print message in 
                                  packed address in position 4-5. 
                                  If position 4-5 is 0 then print the standard message.
                                  Syntax: ([DIR] TO [ROOM] IF [OBJECT] IS OPEN [ELSE "[message]"])
```
In Zork 285 only UEXIT, NEXIT and CEXIT are used.  
  
ZIL functions that operates on the object property tables are; ```GETP```, ```GETPT```, ```MAP-DIRECTIONS```, ```NEXTP```, ```PTSIZE``` and ```PUTP``` 

## How the player moves around, ```WALK```
In MDL when the player types a direction, for example ```n```, this will be parsed into ```PRSVEC``` as ```WALK, NORTH, <>```. The verb ```WALK``` has the ```VFCN = WALK``` so that function will be called. In ZIL ```EPARSE``` will parse the input into ```PRSVEC``` as ```0, W?WALK, W?NORTH, 0``` instead. The line ```<SYNTAX WALK = WALK>``` in [aact.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/aact.zil) is the link between the verb ```WALK```  and the function ```WALK``` in [rooms.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/rooms.zil). How this link, via the syntax line, works in ZIL will be covered in the next part. Here we concentrate on the ```WALK``` function and how it extracts the direction properties from the room objects and moves the player to the new room.  
  
The orginal function in MDL ([ROOMS 154 [Jun 14 1977]](https://https://github.com/MITDDC/zork-1977-07/blob/main/zork/9005183/marc/rooms.154)):
```
<DEFINE WALK ("AUX" LEAVINGS NRM (WHERE <CHTYPE <2 ,PRSVEC> ATOM>) (ME ,WINNER)
            (RM <1 .ME>))
    #DECL ((WHERE) ATOM (ME) ADV (RM) ROOM (LEAVINGS) <OR ROOM CEXIT NEXIT>
           (NRM) <OR FALSE
             <<PRIMTYPE VECTOR> [REST ATOM <OR ROOM NEXIT CEXIT>]>>)
    <COND (<AND <NOT <LIT? .RM>> <PROB 75>>
           <COND (<SET NRM <MEMQ .WHERE <REXITS .RM>>>
              <SET LEAVINGS <2 .NRM>>
              <COND (<AND <TYPE? .LEAVINGS ROOM>
                  <LIT? .LEAVINGS>>
                 <GOTO .LEAVINGS>
                 <ROOM-INFO <>>)
                (<AND <TYPE? .LEAVINGS CEXIT>
                  ,<CXFLAG .LEAVINGS>
                  <LIT? <SET LEAVINGS <CXROOM .LEAVINGS>>>>
                 <GOTO .LEAVINGS>
                 <ROOM-INFO <>>)
                (<TELL
"Dear, dear.  You seem to have fallen into a bottomless pit.">
                 <SLEEP 4>
                 <JIGS-UP <PICK-ONE ,NO-LIGHTS>>)>)
             (<TELL
"Dear, dear.  You seem to have fallen into a bottomless pit.">
              <SLEEP 4>
              <JIGS-UP <PICK-ONE ,NO-LIGHTS>>)>)
          (<SET NRM <MEMQ .WHERE <REXITS .RM>>>
           <SET LEAVINGS <2 .NRM>>
           <COND (<TYPE? .LEAVINGS ROOM>
              <GOTO .LEAVINGS>
              <ROOM-INFO <>>)
             (<TYPE? .LEAVINGS CEXIT>
              <COND (,<CXFLAG .LEAVINGS>
                 <GOTO <CXROOM .LEAVINGS>>
                 <ROOM-INFO <>>)
                (<CXSTR .LEAVINGS> <TELL <CXSTR .LEAVINGS>>)
                (<TELL "There is no way to go in this direction.">)>)
             (T <TELL .LEAVINGS>)>)
          (<TELL "There is no way to go in this direction.">)>>
```
And in psuedo-code:
```
if no light and chance <= 75% then 
    if UEXIT and destination has light then move player to destination.
    elseif CEXIT then
        if flag true and destination has light then move player to destination.
        else print "Dear, dear.  You seem to have fallen into a bottomless pit." and end game.
    else print "Dear, dear.  You seem to have fallen into a bottomless pit." and end game.
elseif UEXIT then move player to destination.
elseif CEXIT then
    if flag true then move player to destination.
    else print message "There is no way to go in this direction."
elseif NEXIT 
    print NEXIT-message
else print
    print message "There is no way to go in this direction."
```
In practice this simply means that you only got a 25% chance to survive if you, in a dark room, walk in direction that has no exit.  
  
Converted to ZIL this becomes ([rooms.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/rooms.zil)):
```
<ROUTINE WALK ("AUX" (RM ,HERE) WHERE PTS NRM STR LEAVINGS)
    <IFFLAG (DEBUG <TELL "WALK" CR>)>
    <AND <GETP .RM ,P?RACTION> <APPLY <GETP .RM ,P?RACTION>>>  ;"Extra call to RACTION with W?WALK (CAROUSEL-ROOM)" 
    <SET WHERE <GETB <2 ,PRSVEC> 7>>  ;"P1? for direction word"
    <COND (<AND <NOT <LIT? .RM>> <PROB 75>>
            <COND (<SET NRM <GETPT .RM .WHERE>>
                <SET PTS <PTSIZE .NRM>>
                <SET LEAVINGS <GET .NRM ,REXIT>>  ;"Where to?"
                <COND (<AND <=? .PTS ,UEXIT>  ;"There's an UEXIT there and the room is lit. 75% chance for success." 
                            <LIT? .LEAVINGS>>
                        <GOTO .LEAVINGS> 
                        <ROOM-INFO <>>) 
                      (<AND <=? .PTS ,CEXIT>  ;"There's an CEXIT there with true FLAG and the room is lit. 75% chance for success."
                            <VALUE <GETB .NRM ,CEXITFLAG>>
                            <LIT? .LEAVINGS>>
                        <GOTO .LEAVINGS> 
                        <ROOM-INFO <>>)
                      (T 
                        <TELL "Dear, dear.  You seem to have fallen into a bottomless pit." CR>
                        <SLEEP 4>
                        <JIGS-UP <PICK-ONE ,NO-LIGHTS>>)>)
                  (T
                    <TELL "Dear, dear.  You seem to have fallen into a bottomless pit." CR>
                    <SLEEP 4>
                    <JIGS-UP <PICK-ONE ,NO-LIGHTS>>)>)
          (<SET NRM <GETPT .RM .WHERE>>
                <SET PTS <PTSIZE .NRM>>
                <SET LEAVINGS <GET .NRM ,REXIT>>                  ;"Where to?"
                <COND (<=? .PTS ,UEXIT> 
                        <GOTO .LEAVINGS> 
                        <ROOM-INFO <>>)
                      (<=? .PTS ,CEXIT>
                        <COND (<VALUE <GETB .NRM ,CEXITFLAG>>       ;"FLAG is true"
                                <GOTO .LEAVINGS> 
                                <ROOM-INFO <>>)
                              (<SET STR <GET .NRM ,CEXITSTR>>      ;"FLAG is false"
                                <TELL .STR CR>)
                              (T 
                                <TELL "There is no way to go in this direction." CR>)>)
                      (<=? .PTS ,NEXIT> 
                        <TELL .LEAVINGS CR>)>)
          (<TELL "There is no way to go in this direction." CR>)>>
```
Remember that the below constants have these values from the property definition of ```DIRECTIONS``` (above):
```
<CONSTANT UEXIT 2>
<CONSTANT NEXIT 3>
<CONSTANT CEXIT 5>
<CONSTANT REXIT 0>
<CONSTANT NEXITSTR 0>
<CONSTANT CEXITFLAG 4>
<CONSTANT CEXITSTR 1>
```
The conversion to ZIL is pretty straightforward and I will just point out a couple of things:
* The line ```<AND <GETP .RM ,P?RACTION> <APPLY <GETP .RM ,P?RACTION>>>``` is there to make an extra call to the room action-routine, if there is one, before we leave the room. This is there to give the "Carousel Room" opportunity to scramble the exits before we extract the exit.
* ```<SET WHERE <GETB <2 ,PRSVEC> 7>>``` puts the directions property number in ```WHERE```.
* ```<SET NRM <GETPT .RM .WHERE>>``` puts that direction property on current room into ```NRM```.
* ```<SET PTS <PTSIZE .NRM>>``` puts the size of the property, i.e. if the exit is a UEXIT, NEXIT or CEXIT, into ```PTS```.
* ```LEAVINGS``` is the new room (or the NEXIT string).
* ```LIT?```, ```ROOM-INFO``` and ```GOTO``` are helper functions that pretty much do what they are called.  
  
Next: Part 7 - Matching the syntax
