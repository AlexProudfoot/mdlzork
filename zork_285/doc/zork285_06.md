# Recreating Zork 285 in ZIL - Part 6 - Walking around
This is part 6 in an ongoing series. The previous part: "Part 5 - Parsing the input" is [here](https://github.com/heasm66/mdlzork/blob/master/zork_285/doc/zork285_05.md).  
  
In this part we will examine how the properties are stored in the z-machine, available ZIL functions and how to move the player around.
## The object table and the property tables
The object table and the property tables are explained in [chapter 12 of The Z-Machine Standards Document](https://www.inform-fiction.org/zmachine/standards/z1point0/sect12.html) and I recommend reading it for more details.  
  
The start of the table is specified in bytes 10-11 of the header. If you want you can get this value in ZIL by either ```<LOWCORE OBJECT>``` or simply ```<GET 0 5>``` (get fifth WORD) but this is a value you usually don't need. At this address follows in order the property default table, the object tree table and the properties table.

There are differences between version 3 and the later varsions that will affect the size of these tables. These differences are:
* Version 3 only allows up to 32 attribute flags, later versions can have up to 48.
* Version 3 only allows up to 255 objects, later versions can have up to 65535 (one byte versus two bytes).
* Version 3 only allows up to 31 properties, later versions can have up to 63.
Here we will primarely look at the later versions.  
  
If we take this program:
```
<VERSION XZIP>
<CONSTANT RELEASEID 1>

<DIRECTIONS NORTH EAST WEST SOUTH UP DOWN>

<GLOBAL MAGIC-FLAG <>>

<ROUTINE WINDOW-EXIT () <RTRUE>>
<ROUTINE LIVING-ROOM-F () <RTRUE>>

;"If defined, this CONSTANT is assigned the highest object number during compilation."
<CONSTANT LAST-OBJECT <>> 

<OBJECT ROOMS>

<ROOM LIVING-ROOM  ;"ROOM and OBJECT are synonyms for same function."
      (IN ROOMS)
      (DESC "Living Room")
      (LDESC "You are in the living room. There is a doorway to the east.")
      (NORTH "You can't enter the chimney!")
      (EAST TO KITCHEN)
      (WEST TO STRANGE-PASSAGE IF MAGIC-FLAG ELSE "The door is nailed shut.")
      (DOWN TO CELLAR IF TRAP-DOOR IS OPEN)
      (SOUTH PER WINDOW-EXIT)
      (ACTION LIVING-ROOM-F)
      (FLAGS LANDBIT LIGHTBIT)>  

<ROOM KITCHEN
      (IN ROOMS)
      (DESC "Kitchen")
      (WEST TO LIVING-ROOM)>

<ROOM STRANGE-PASSAGE
      (IN ROOMS)
      (DESC "Strange Passage")>

<ROOM CELLAR
      (IN ROOMS)
      (DESC "Cellar")>
      
<OBJECT LANTERN
    (DESC "brass lantern")
    (IN LIVING-ROOM)
    (SYNONYM de)
    (ADJECTIVE BRASS)>

<OBJECT SWORD
    (IN LIVING-ROOM)
    (DESC "sword")
    (SYNONYM SWORD)>

<OBJECT TRAP-DOOR
    (DESC "trap-door")
    (IN LIVING-ROOM)
    (SYNONYM TRAP-DOOR DOOR TRAPDOOR)
    (FLAGS OPENBIT)>
    
<ROUTINE GO () 
    <CRLF>
    <TELL "<LOWCORE OBJECT>=" N <LOWCORE OBJECT> CR>
    <TELL "P?NORTH=" N ,P?NORTH CR>
    <TELL "P?EAST=" N ,P?EAST CR>   
    <TELL "P?WEST=" N ,P?WEST CR>   
    <TELL "P?SOUTH=" N ,P?SOUTH CR> 
    <TELL "P?UP=" N ,P?UP CR>   
    <TELL "P?DOWN=" N ,P?DOWN CR>   
    <TELL "P?SYNONYM=" N ,P?SYNONYM CR> 
    <TELL "P?ADJECTIVE=" N ,P?ADJECTIVE CR> 
    <TELL "P?LDESC=" N ,P?LDESC CR> 
    <TELL "P?ACTION=" N ,P?ACTION CR>   
    <TELL "ROOMS=" N ,ROOMS CR> 
    <TELL "LIVING-ROOM=" N ,LIVING-ROOM CR> 
    <TELL "KITCHEN=" N ,KITCHEN CR> 
    <TELL "STRANGE-PASSAGE=" N ,STRANGE-PASSAGE CR> 
    <TELL "CELLAR=" N ,CELLAR CR>   
    <TELL "LANTERN=" N ,LANTERN CR> 
    <TELL "SWORD=" N ,SWORD CR> 
    <TELL "TRAP-DOOR=" N ,TRAP-DOOR CR> 
    <TELL "LAST-OBJECT=" N ,LAST-OBJECT CR>>
```
Compiling and running it produces this output:
```
<LOWCORE OBJECT>=282
P?NORTH=63
P?EAST=62
P?WEST=61
P?SOUTH=60
P?UP=59
P?DOWN=58
P?SYNONYM=57
P?ADJECTIVE=56
P?LDESC=55
P?ACTION=54
ROOMS=8
LIVING-ROOM=7
KITCHEN=6
STRANGE-PASSAGE=5
CELLAR=4
LANTERN=3
SWORD=2
TRAP-DOOR=1
LAST-OBJECT=8
```

### The property default table
This table consists of 31 (62 bytes) property default values for version 3 and 63 (126 bytes) for version 4 and later. These are the values returned when reading a property on an object that don't have that property defined. The default values are initialized to 0 (false) but can be set by ```PROPDEF```. ```<PROPDEF SIZE 5>``` will give all objects SIZE=5 if not explicitly defined oherwise.  
  
In our above example the property default table starts at address 282 ($011A) and contains 126 bytes of 0, meaning that all 63 properties have a default value of 0.
```
011A-0197 00 00 00 00 ... 00 00
```
Relevant ZIL functions: ```PROPDEF```
### The object tree table
This table follows directly after the property defaults table and consists of entries of 9 bytes (version 3) or 14 bytes (version 4 or later) for each object. The first entry is object number 1 (object number 0 is considered "nothing").
  
The layout of the table (from *The Z-Machine Standards Document*) are:
```
Version 3:
    the 32 attribute flags    parent     sibling     child   properties
   ---32 bits in 4 bytes---   ---3 bytes------------------  ---2 bytes--

Version 4 and later:
    the 48 attribute flags    parent     sibling     child   properties
   ---48 bits in 6 bytes---   ---3 words, i.e. 6 bytes----  ---2 bytes--   
```
Returning to our example, we get this:
```
Byte 0-5                 6-7     8-9     10-11   12-13
     48 bit flags        Parent  Sibling Child   Properties
0198 00 00 00 00 00 01   00 07   00 02   00 00   02 08      Object 1 TRAP-DOOR, Bit 0 (OPENBIT) is set
01A6 00 00 00 00 00 00   00 07   00 00   00 00   02 18      Object 2 SWORD
01B4 00 00 00 00 00 00   00 07   00 01   00 00   02 21      Object 3 LANTERN
01B2 00 00 00 00 00 00   00 08   00 05   00 00   02 36      Object 4 CELLAR
01D0 00 00 00 00 00 00   00 08   00 06   00 00   02 3E      Object 5 STRANGE-PASSAGE
01DE 00 00 00 00 00 00   00 08   00 00   00 00   02 4C      Object 6 KITCHEN
01EC 00 00 00 00 00 06   00 08   00 04   00 03   02 57      Object 7 LIVING-ROOM, Bit 1 and 2 are set (LIGHTBIT & LANDBIT)
01FA 00 00 00 00 00 00   00 00   00 00   00 07   02 86      Object 8 ROOMS
```
It is easy to identify ```ROOMS``` at the top in the object tree, it points to its first child, ```LIVING-ROOM```, that in its turn points to a sibling chain, ```LIVING-ROOM --> CELLAR --> STRANGE-PASSAGE --> KITCHEN```, and its first child, ```LANTERN```, and so on.  
  
Note that flags don't need to be explicitly defined, instead the compiler assigns them a free slot during compilation. In the above table we can see that ```OPENBIT```, ```LIGHTBIT``` and ```LANDBIT``` are assigned bits 0-2, respectively.  
  
Relevant ZIL functions: ```FIRST?```, ```IN?```, ```LOC```, ```MAP-CONTENTS```, ```MOVE```, ```NEXT?```, ```PRINTD``` and ```REMOVE``` 
### The property tables
Each object has its own property table starting at the address from the object tree table above (byte 7-8 for version 3 and byte 12-13 for later versions). Each table starts with a header that contains the short description of the object (the ```DESC```-part). The layout of the header is (from ยง12.4 of *The Z-Machine Standards Document*):
```
   text-length     text of short name of object
  -----byte----   --some even number of bytes---
```
ยง12.4 also states "where the text-length is the number of 2-byte words making up the text, which is stored in the usual format. (This means that an object's short name is limited to 765 Z-characters.)". The limit of 765 z-chars isn't entirely correct because it is perfectly legal to use abbreviations and abbreviations don't have that limit, you could circumvent the limitation if you really want it.  
  
After the header follows all the properties for this object in descending order. Property 0 indicates the end of this table. The layout of each property (again from *The Z-Machine Standards Document*) is:
```
Version 3:
   size byte     the actual property data
                ---between 1 and 8 bytes--

Version 4 and later: 
   size and number       the actual property data
  --1 or 2 bytes---     --between 1 and 64 bytes--
```
for version 3 the *size byte* have the property number in bits 0-4 (1-31) and the property size in bits 5-7 (add 1 to this value to get actual size of 1-8).  
  
Version 4 and later can have 1 or 2 bytes for size and number. Bit 7 of the first byte tells if it is one or two bytes. If bit 7 is 0 then it is only one byte, bit 6 tells the property size (0=1 byte, 1=2 bytes) and bits 0-5 is the property number. If bit 7 is 1 then it is two bytes, bit 6 is unused but always clear by convention and bits 0-5 is the size of the property. For the second byte, bits 6-7 are unused but bit 7 is always set by convention and bit 6 is set in ZIL and clear in Inform, bits 0-5 are the property number.
  
If we return to the example:
```
0208 03 66 E6 54 BC 84 45               Length=5 2-byte words, "trap-door" 
     B9 86 02 FD 02 B5 03 06            89 86 --> Prop#=57, Size=6, W?TRAP-DOOR W?DOOR W?TRAPDOOR
     00                                 Nothing, end of properties marker

0218 02 63 94 DD 25                     Length=2 2-byte words, "sword"
     79 02 F4                           79 --> Prop#=57, Size=2, W?SWORD
     00                                 Nothing, end of properties marker

0221 05 1E E6 63 00 44 D3 65 57 CC A5   Length=5 2-byte words, "brass lantern"
     B9 84 02 D9 02 D0                  89 84 --> Prop#=57, Size=4, W?LANTERN W?LAMP
     78 02 AC                           78    --> Prop#=56, Size=2, W?BRASS
     00                                 Nothing, end of properties marker

0236 03 11 0A 46 26 DC A5               Length=3 2-byte words, "Cellar"
     00                                 Nothing, end of properties marker

023E 06 13 19 5C D3 31 40 12 A6 63 06 B1 45 Length=6 2-byte words, "Strange Passage"
     00                                     Nothing, end of properties marker

024C 03 12 0E 65 0D AA 65               Length=3 2-byte words, "Kitchen"
     7D 00 07                           7D    --> Prop#=61, Size=2, UEXIT: goto room 7
     00                                 Nothing, end of properties marker

0257 05 12 2E 6D D3 30 04 5E 94 C8 A5   Length=5 2-byte words, "Living Room"
     BF 83 01 3A 00                     BF 83 --> Prop#=63, Size=3, NEXIT: Message at 4x$013A
     7E 00 06                           7E    --> Prop#=62, Size=2, UEXIT: goto room 6
     BD 85 00 05 01 2B 10               BD 85 --> Prop#=61, Size=5, CEXIT: goto room 5 if variable 10 (MAGIC-FLAG) is true else print message at 4x$012B
     BC 84 00 C9 00 00                  BC 84 --> Prop#=60, Size=4, FEXIT: goto room returned by routine at 4x$00C9
     BA 86 00 04 00 01 00 00            8A 86 --> Prop#=58, Size=6, DEXIT: goto room 4 if object 1 has bit 1 set (OPENBIT) else print message at $0000 (no msg)
     77 01 2F                           77    --> Prop#=55, Size=2, LDESC: Message at 4x$012F
     76 00 CA                           76    --> Prop#=54, Size=2
     00                                 Nothing, end of properties marker

0286 01 94 A5                           Length=1 2-byte words, ""
     00                                 Nothing, end of properties marker
```
Examining the first objects at $0208 in more detail: The header is ```03 66 E6 54 BC 84 45``` and this means that the text length is 3 2-byte words and they translate to binary as ```0 11001='t' 10111='r' 00110='a' 0 10101='p' 00101=shift to A2 11100='-' 1 00001 00010=use abbrev 2 ('door') 00101=padding```. The first property is ```B9 86 02 FD 02 B5 03 06```.```$B9 = 1 0 111001```, meaning bit 7 is set (size in next byte) and property number is 57 (bits 0-5). ```$86 = 1 0 000110``` means that the size is 6 bytes (bits 0-5). The ```02 FD 02 B5 03 06``` is pointers to ```W?TRAP-DOOR```, ```W?DOOR``` and ```W?TRAPDOOR``` in the vocabulary, respectively. ```$00 = property 0``` means that this is the end of this property table.  

The next object, "sword", at $0218 have it first property ```79 02 F4```. The size and number byte is ```$79 = 0 1 111001```. Because bit 7 is clear the size is in bit 6 that is set, meaning a size of 2 bytes. The property number in bits 0-5 is 57. The property data is a pointer to the word ```W?SWORD``` in the vocabulary.  
### Directions in the property data
There are five different type of exits that can be attached to a direction property, defined in the table below:
```
Name  Size 
UEXIT   2  "Unconditioal exit" - goto the room stored in property position 0-1 
                                 Syntax: ([DIR] TO [ROOM]) 
NEXIT   3  "Non-exit"          - no exit but print message at packed address in property position 0-1 
                                 instead of standard message. Padded with 0 in position 2 to get to size=3.
                                 Syntax: ([DIR] "[message]")
FEXIT   4  "Function exit"     - goto the room returned by routine at packed address in property position 0-1.
                                 If function returns false, don't move. Padded with 0 in position 2-3 to get to size=4.
                                 Syntax: ([DIR] PER [ROUTINE-EXIT]) 
CEXIT   5  "Conditional exit"  - goto the room stored in property position 0-1 if value in variable in position 4 is true
                                 else print message in packed address in position 2-3. If position 2-3 is 0 then print
                                 the standard message.
                                 Syntax: ([DIR] TO [ROOM] IF [VARIABLE] [ELSE "[message]"])
DEXIT   6  "Door exit"         - goto the room stored in property position 0-1 if object with object number stored in 
                                 position 2-3 have OPENBIT set else print message in packed address in position 4-5. 
                                 If position 4-5 is 0 then print the standard message.
                                 Syntax: ([DIR] TO [ROOM] IF [OBJECT] IS OPEN [ELSE "[message]"])
```
As you probably already noticed the different exit types have different sizes. This is by design and the size is used to distinguish which type of exit it is.  
  
Relevant ZIL functions: ```GETP```, ```GETPT```, ```MAP-DIRECTIONS```, ```NEXTP```, ```PTSIZE``` and ```PUTP``` 
