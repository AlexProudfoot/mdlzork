# Recreating Zork 285 in ZIL - Part 5 - Parsing the input
This is part 5 in an ongoing series. The previous part: Part 4 - The main loop is [here](https://github.com/heasm66/mdlzork/blob/master/zork_285/doc/zork285_04.md).  
  
In this part we will examine how the vocabulary is organized in the z-machine and how to parse the players input.
## Vocabulary entries in the Z-machine
For Z-machines version 4 and later a word occupies 9 bytes for each entry in the vocabulary, 6 bytes for z-chars and 3 data bytes. For version 3 and 
earlier only 4 bytes are used for z-chars, making each entry 7 bytes long. The first data byte is used for the part-of-speech flags and the other two have
different uses for different word types.

If we define the following (ZIL):
```
<SYNTAX TAKE = V-TAKE>
<SYNONYM TAKE GET>
<BUZZ THE AN A>
<DIRECTIONS NORTH EAST WEST SOUTH>
<OBJECT LANTERN
    (SYNONYM LANTERN LAMP)
    (ADJECTIVE USELESS)>
```
The words end up in the vocabulary like this:
```
Version 4 and later:
Byte                                  Byte 0-5                                                              Byte 6                    Byte 7    Byte 8
  0   1   2   3   4   5   6   7   8   Up to 9 z-chars padded with z-char 5 (00101, ^ here)                  Part-of-speech flag       V1        V2                                                            
100 208  40 165 148 165  65 255   0 = 0 11001 00110 10000 0 01010 00101 00101 1 00101 00101 00101 take^^^^^ P1?VERB+PS?VERB              255         0  
 49  89  20 165 148 165  65 255   0 = 0 01100 01010 11001 0 00101 00101 00101 1 00101 00101 00101 get^^^^^^ P1?VERB+PS?VERB              255         0
 68 211 101  87 204 165 128   1   0 = 0 10001 00110 10011 0 11001 01010 10111 1 10011 00101 00101 lantern^^ PS?OBJECT                      1         0
 68 210  84 165 148 165 128   1   0 = 0 10001 00110 10010 0 10101 00101 00101 1 00101 00101 00101 lamp^^^^^ PS?OBJECT                      1         0
107  10  69  88 224 165  32   0   0 = 0 11010 11000 01010 0 10001 01010 11000 1 11000 00101 00101 useless^^ PS?ADJECTIVE                   0         0
101 170  20 165 148 165   4 255   0 = 0 11001 01101 01010 0 00101 00101 00101 1 00101 00101 00101 the^^^^^^ PS?BUZZ-WORD                 255         0
 26 101  20 165 148 165   4 254   0 = 0 00110 10011 00101 0 00101 00101 00101 1 00101 00101 00101 an^^^^^^^ PS?BUZZ-WORD                 254         0
 24 165  20 165 148 165   4 253   0 = 0 00110 00101 00101 0 00101 00101 00101 1 00101 00101 00101 a^^^^^^^^ PS?BUZZ-WORD                 253         0
 78 151 101 165 148 165  19  63   0 = 0 10011 10100 10111 0 11001 01101 00101 1 00101 00101 00101 north^^^^ P1?DIRECTION+PS?DIRECTION     63         0
 40 216 100 165 148 165  19  62   0 = 0 01010 00110 11000 0 11001 00101 00101 1 00101 00101 00101 east^^^^^ P1?DIRECTION+PS?DIRECTION     62         0
113  88 100 165 148 165  19  61   0 = 0 11100 01010 11000 0 11001 00101 00101 1 00101 00101 00101 west^^^^^ P1?DIRECTION+PS?DIRECTION     61         0
 98 154 101 165 148 165  19  60   0 = 0 11000 10100 11010 0 11001 01101 00101 1 00101 00101 00101 south^^^^ P1?DIRECTION+PS?DIRECTION     60         0

Version 3:
Byte                          Byte 0-3                                       Byte 4                   Byte 5    Byte 6
  0   1   2   3   4   5   6   Up to 6 z-chars                                Part-of-speech flag       V1        V2                                                            
100 208 168 165  65 255   0 = 0 11001 00110 10000 1 01010 00101 00101 take^^ P1?VERB+PS?VERB             255         0  
 49  89 148 165  65 255   0 = 0 01100 01010 11001 1 00101 00101 00101 get^^^ P1?VERB+PS?VERB             255         0
 68 211 229  87 128   1   0 = 0 10001 00110 10011 1 11001 01010 10111 lanter PS?OBJECT                     1         0
 68 210 212 165 128   1   0 = 0 10001 00110 10010 1 10101 00101 00101 lamp^^ PS?OBJECT                     1         0
107  10 197  88  34 255   0 = 0 11010 11000 01010 1 10001 01010 11000 useles PS?ADJECTIVE                  0         0
101 170 148 165   4 255   0 = 0 11001 01101 01010 1 00101 00101 00101 the^^^ PS?BUZZ-WORD                255         0
 26 101 148 165   4 254   0 = 0 00110 10011 00101 1 00101 00101 00101 an^^^^ PS?BUZZ-WORD                254         0
 24 165 148 165   4 253   0 = 0 00110 00101 00101 1 00101 00101 00101 a^^^^^ PS?BUZZ-WORD                253         0
 78 151 229 165  19  63   0 = 0 10011 10100 10111 1 11001 01101 00101 north^ P1?DIRECTION+PS?DIRECTION    63         0
 40 216 228 165  19  62   0 = 0 01010 00110 11000 1 11001 00101 00101 east^^ P1?DIRECTION+PS?DIRECTION    62         0
113  88 228 165  19  61   0 = 0 11100 01010 11000 1 11001 00101 00101 west^^ P1?DIRECTION+PS?DIRECTION    61         0
 98 154 229 165  19  60   0 = 0 11000 10100 11010 1 11001 01101 00101 south^ P1?DIRECTION+PS?DIRECTION    60         0

Byte 0-3: Word of 6 (v3) or 9 (v4-) z-chars. Words are padded with the shift z-char 5 (00101) to fill up.  
or   0-5: First bit of each WORD (2 bytes) is 0 except for the last WORD where it is 1 to indicate the end.

Byte 6  : The part-of-speech flags with the following meaning:
            P1?OBJECT       0   Object data stored in V1.
            P1?VERB         1   Verb data stored in V1 if set, otherwise in V2.
            P1?ADJECTIVE    2   Adjective data stored in V1 if set, otherwise in V2.
            P1?DIRECTION    3   Direction data stored in V1 if set, otherwise in V2.
            PS?BUZZ-WORD    4   Word type buzz. Word data stored in V1 contains 
                                buzz-word number counting downwards from 255.
            PS?PREPOSITION  8   Word type preposition. Word data stored in V1 contains 
                                preposition number counting downwards from 255.
            PS?DIRECTION   16   Word type direction. Word data stored in V1 or V2 contains 
                                direction number counting downwards from 63.
            PS?ADJECTIVE   32   Word type adjective. Word data stored in V1 or V2 contains 
                                adjective number counting downwards from 255 in version 3, 
                                always 0 in later versions.
            PS?VERB        64   Word type verb. Word data stored in V1 or V2 contains 
                                verb number counting downwards from 255.
            PS?OBJECT     128   Word type object. Word data stored in V1 or V2. 
                                Word data is always 1.
```
To illustrate how the flags are used, consider that your game needs to understand the following sentence:  
  
"Plant the potplant in the plant pot"   (version 3)  
"Plant the pot plant in the plant pot"  (version 4 and later)  
  
The "plant" is a verb and an adjective in version 3. In version 4 and later it is also an object.
This ends up in the vocabulary as this:
```
Version 3:
Byte                          Byte 0-3                                       Byte 4                         Byte 5  Byte 6
  0   1   2   3   4   5   6   Up to 6 z-chars                                Part-of-speech flag                V1        V2                                                            
 86  38 207  37  97 254 255 = 0 10101 10001 00110 1 10011 11001 00101 plant^ P1?VERB+PS?VERB+PS?ADJECTIVE      254       255    

"plant" is both verb and adjective. P1?VERB means that verb number is stored in V1 and adjective number in V2.

Version 4-
Byte                                  Byte 0-5                                                              Byte 6                    Byte 7    Byte 8
  0   1   2   3   4   5   6   7   8   Up to 9 z-chars                                                       Part-of-speech flag       V1        V2                                                            
 86  38  79  37 148 165 225 254   1 = 0 10101 10001 00110 0 10011 11001 00101 1 00101 00101 00101 plant^^^^ PS?OBJECT+PS?VERB+           254         1  
                                                                                                            PS?ADJECTIVE+P1?VERB

"plant" is verb, object and adjective. P1?VERB means that verb number is stored in V1 and object data in V2, adjective data (0) is discarded.
``` 
Note that the ```P1?```-part always is defined for word types that have word numbers.

In Infocom's games there is often a function to extract word type and word number, ```WT?```:
```
;"Check whether word pointed at by PTR is the correct part of speech.
   The second argument is the part of speech (,PS?<part of speech>).  The
   3rd argument (,P1?<part of speech>), if given, causes the value
   for that part of speech to be returned."

<ROUTINE WT? (PTR BIT "OPTIONAL" (B1 5) "AUX" (OFFS ,P-P1OFF) TYP)
    <COND (<BTST <SET TYP <GETB .PTR ,P-PSOFF>> .BIT>
           <COND (<G? .B1 4> <RTRUE>)
             (T
              <SET TYP <BAND .TYP ,P-P1BITS>>
              <COND (<NOT <EQUAL? .TYP .B1>> <SET OFFS <+ .OFFS 1>>)>
              <GETB .PTR .OFFS>)>)>>
```
```WT?``` works like this:
```
<WT? ,W?TAKE ,PS?VERB>               ---> Returns 1 (True)
<WT? ,W?TAKE ,PS?OBJECT>             ---> Returns 0 (False)
<WT? ,W?TAKE ,PS?VERB ,P1?VERB>      ---> Returns 255 (Word-ID)
```
Zork 285 have a simpler version of ```WT?``` and a function, ```W=?``` ([defs.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/defs.zil)), 
that compares two words and return true if they are the same. They assume that the word number is stored in V1.
```
;"WORD of TYPE?"
<ROUTINE WT? (WORD PS)
    <COND (<0? .WORD> <RFALSE>)>
    <COND (<BTST <GETB .WORD 6> .PS> <RTRUE>)  ;"6 = Part of speach flags for EZIP-"
          (T <RFALSE>)>>

;"Word-2 is the same or synonym to word-2"
<ROUTINE W=? (W1 W2)
    <COND (<AND <OR <AND <WT? .W1 ,PS?VERB> <WT? .W2 ,PS?VERB>>
                    <AND <WT? .W1 ,PS?DIRECTION> <WT? .W2 ,PS?DIRECTION>>>
                <=? <GETB .W1 7> <GETB .W2 7>>>
                    <RTRUE>)
          (<AND <WT? .W1 ,PS?OBJECT> <WT? .W2 ,PS?OBJECT>
                <=? <FIND-OBJ .W1> <FIND-OBJ .W2>>>
                    <RTRUE>)
          (<AND <WT? .W1 ,PS?ADJECTIVE> <WT? .W2 ,PS?ADJECTIVE>
                <=? .W1 .W2>>
                    <RTRUE>)
          (T <RFALSE>)>>

<WT? ,W?TAKE ,PS?VERB>      --> True
<WT? ,W?TAKE ,PS?OBJECT>    --> False
<W=? ,W?TAKE ,W?GET>        --> True
<W=? ,W?TAKE ,W?LAMP>       --> False
```
## Splitting the input string - LEX
The ```LEX``` function ([```ROOMS 154 [Jun 14 1977]```](https://https://github.com/MITDDC/zork-1977-07/blob/main/zork/9005183/marc/rooms.154)) is quite simple. It takes an input string and splits it in up to 10 separate uppercase words of up to 5 characters long
and stores them in the string vector, ```LEXV```.
```
<LEX "throw the knife at the troll">$
,LEXV$
["THROW" "THE" "KNIFE" "AT" "THE" "TROLL" "" "" "" ""]
```
On ZIL, or the z-machine, there is no string datatype so this have to be handled in a different way with ```READ``` and ```LEX``` 
([read](https://www.inform-fiction.org/zmachine/standards/z1point1/sect15.html#sread) 
and [tokenize](https://www.inform-fiction.org/zmachine/standards/z1point1/sect15.html#tokenize) in Inform). ```READ``` and ```LEX``` are described in more
detail in the [YZIP Specifications](https://github.com/heasm66/YZIP-Specifications/raw/master/yzip%20specifications.pdf) but in short the two
byte-tables, ```INBUF``` and ```LEXV```, ends up like this after a ```READ``` (byte 0, the max length and max number of words, are both tables initialized in ```RDCOM```):
```
INBUF contains:
    Byte 0   Max number of chars read into the buffer
         1   Actual number of chars read into the buffer
         2-  The typed chars all converted to lowercase

LEXV contains:
    Byte 0   Max number of words parsed
         1   Actual number of words parsed
         2-3 Address to first word in vocabulary (0 if word is not in it)
         4   Length of first word
         5   Start position (in INBUF) of first word
         6-9 Second word
         ...

If the string "throw the knife at the troll" is typed into the READ input, the result in INBUF and LEXV are:

INBUF:
 Byte 0   1   2   3   4   5   6   7   8   9  10  11  12  13  14  15  16  17  18  19  20  21  22  23  24  25  26  27  28  29  
    100  28  't' 'h' 'r' 'o' 'w' ' ' 't' 'h' 'e' ' ' 'k' 'n' 'i' 'f' 'e' ' ' 'a' 't' ' ' 't' 'h' 'e' ' ' 't' 'r' 'o' 'l' 'l' 
LEXV:
 Byte 0   1 2-3     4 5 6-7   8 9 10-11   12 13 14-15 16 17 18-19 20 21 22-23   24 25
     10   6 W?THROW 5 2 W?THE 3 8 W?KNIFE  5 12 W?AT   2 18 W?THE  3 21 W?TROLL  5 25

LEX does only the tokenization on and existing byte-table exactly as READ but without the input.
```
Finally, ZIL allow and recognizes words up to 9 characters and the original MDL only uses 5 characters. This 
"dumbing down" of the parsing is done by copying the original input to ```RAWBUF```, replace all characters in ```INBUF``` beyond 5 with a space
and then redo the tokenization (the characters ```.,:;?!``` are also replaced by space and ignored, i.e. hardcoded buzz-words).
```
;"Inputs text and puts the result in INBUF and LEXV"
<ROUTINE LEX ("AUX" LEN WS WE WC)
    <DO (I 1 ,INBUF-SIZE) <PUTB ,INBUF .I 0>>  ;"Clear INBUF"
    <DO (I 1 ,LEXV-SIZE) <PUTB ,LEXV .I 0>>    ;"Clear LEXV"
    <READ ,INBUF ,LEXV>
    ;"Some (Lectrote 1.3.5/Parchment) terps that uses ZVM don't preserve this 
      when resuming from an autosave. This makes sure it's reset between sessions"
    <FIXED-FONT-ON>  
    <DO (I 1 ,INBUF-SIZE) <PUTB ,RAWBUF .I <GETB ,INBUF .I>>>  ;"Copy INBUF to RAWBUF beefore manipulating it."
    ;"Replace all .,:;?! in input with SPACE, See BRKS in ROOMS.154..LEX"
    <SET LEN <+ <GETB ,INBUF 1> 1>>
    <DO (I 0 .LEN) 
        <COND (<OR <=? <GETB ,INBUF .I> !\.>
                   <=? <GETB ,INBUF .I> !\,>
                   <=? <GETB ,INBUF .I> !\:>
                   <=? <GETB ,INBUF .I> !\;>
                   <=? <GETB ,INBUF .I> !\!>
                   <=? <GETB ,INBUF .I> !\?>> <PUTB ,INBUF .I 32>)>>
    ;"Limit words in input to max 5 characters"
    <SET WC <GETB ,LEXV 1>> ;"# of parsed words"
    <DO (I 1 .WC)
        <SET LEN <GETB ,LEXV <* .I 4>>>
        <SET WS <GETB ,LEXV <+ <* .I 4> 1>>>
        <SET WE <+ .WS <- .LEN 1>>>
        <COND (<G? .LEN 5>
            <SET WS <+ .WS 5>>
            <DO (J .WS .WE) <PUTB ,INBUF .J 32>>)>>     
    ;"Redo parsing"
    <LEX ,INBUF ,LEXV>>
```
## And finally... The parsing - EPARSE
In MDL the ```EPARSE``` function ([```ROOMS 154 [Jun 14 1977]```](https://https://github.com/MITDDC/zork-1977-07/blob/main/zork/9005183/marc/rooms.154)) takes the vector ```LEXV``` and tries to identify a verb (PRSA), a direct object (PRSO) and an indirect object (PRSI). 
If the parsing is successful ```EPARSE``` returns true and the vector ```PRSVEC``` contains the PRSA in the first slot, PRSO in the second and PRSI in the third.
If the parsing fails, an error message is printed and ```EPARSE``` returns false. The error messages can be turned off if the input parameter, ```SILENT?```, is 
true (this is used by the "Echo Room").
```
<LEX "Throw the knife at the troll">$
["THROW" "THE" "KNIFE" "AT" "THE" "TROLL" "" "" "" ""]
<EPARSE ,LEXV <>>$
T
,PRSVEC$
[#VERB [THROW!-WORDS DROP T] #OBJECT [KNIFE knife] #OBJECT [TROLL ]]
```
With directions, the verb ```WALK``` is inserted as PRSA.
``` 
<LEX "n">$
["N" "" "" "" "" "" "" "" "" ""]
<EPARSE ,LEXV <>>$
T
,PRSVEC$
[#VERB [WALK!-WORDS WALK <>] #DIRECTION NORTH!-WORDS #FALSE ()]
```
The following parser errors are handled:
```
Missing verb but have object:                           "What should I do with it?"
Missing verb and unknown word:                          "I don't know how to do that."
Direction at wrong position or unexpected:              "I can't parse that."
Unknown word:                                           "I don't know that word."
No verb and no object:                                  "Huh?"
Verb not WALK and PRSO is direction:                    "You can't do that!"
Verb is WALK and PRSO is not direction:                 "Go where?"
Illegal number of arguments for this verb:              Print VSTR
Illegal number of arguments for this verb (no VSTR):    "I don't understand."
```
Examples:
```
Welcome to adventure.
You are in an open field west of a big white house, with a closed, locked
front door.
>lamp
What should I do with it?
>sword
I don't know how to do that.
>get troll south
I can't parse that.
>get sword
I don't know that word.
>the
Huh?
>get south
You can't do that!
>go troll
Go where?
>tie
Tie what?
>walk
I don't understand.
>
```
The ```VARGS, VSTR, VACTION? and VMAX``` are attributes defined along with the verb and stored in the verb datatype, 
see [Part 2 - Extracting the dictionary](https://github.com/heasm66/mdlzork/blob/master/zork_285/doc/zork285_02.md). 
In ZIL they are redefined as functions ([defs.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/defs.zil)) that returns the same data. 
```VNO``` and ```VNO=?``` are helper functions. ```VNO``` simply extracts the ```V1``` byte from a vocabulary entry and ```VNO=?``` compares up to 7 ```V1``` bytes of entries and returns
true if they all are the same.   
```
;"VARGS, VSTR, VACTION?, VMAX

  In MDL this is stored along with the verb. ZIL has a more sophisticated
  way of handling syntax. These routines returns the right value for the
  different verbs as defined in the original."
<ROUTINE VARGS (V)
    <COND (<OR <VNO=? .V ,W?WALK ,W?UNTIE ,W?PUSH ,W?POUR ,W?READ> 
               <VNO=? .V ,W?WAVE ,W?TIE>> 
             <RETURN 1>)
          (T <RETURN 0>)>>

<ROUTINE VMAX (V)
    <COND (<VNO=? .V ,W?WALK> <RETURN 1>)
          (T <RETURN 2>)>>
          
<ROUTINE VACTION? (V)
    <COND (<VNO=? .V ,W?WALK> <RFALSE>)
          (T <RTRUE>)>>

<ROUTINE VSTR (V)
    <COND (<VNO=? .V ,W?TAKE> <TELL "Take what?" CR>)
          (<VNO=? .V ,W?THROW> <TELL "Throw what?" CR>)
          (<VNO=? .V ,W?UNTIE> <TELL "Untie what?" CR>)
          (<VNO=? .V ,W?GIVE> <TELL "Give what?" CR>)
          (<VNO=? .V ,W?PUSH> <TELL "Push what?" CR>)
          (<VNO=? .V ,W?MOVE> <TELL "Move what?" CR>)
          (<VNO=? .V ,W?POUR> <TELL "Pour what?" CR>)
          (<VNO=? .V ,W?READ> <TELL "Read what?" CR>)
          (<VNO=? .V ,W?WAVE> <TELL "Wave what?" CR>)
          (<VNO=? .V ,W?TIE> <TELL "Tie what?" CR>)
          (<VNO=? .V ,W?DROP> <TELL "Drop what?" CR>)
          (T <RFALSE>)>>

<ROUTINE VNO (V) <RETURN <GETB .V 7>>>

<ROUTINE VNO=? (V1 V2 "OPT" V3 V4 V5 V6 V7) 
    <COND (<OR <=? <VNO .V1> <VNO .V2>>
               <AND <N=? .V3 0> <=? <VNO .V1> <VNO .V3>>>
               <AND <N=? .V4 0> <=? <VNO .V1> <VNO .V4>>>
               <AND <N=? .V5 0> <=? <VNO .V1> <VNO .V5>>>
               <AND <N=? .V6 0> <=? <VNO .V1> <VNO .V6>>>
               <AND <N=? .V7 0> <=? <VNO .V1> <VNO .V7>>>>
             <RTRUE>)
          (T <RFALSE>)>>
```
Pseudo-code for EPARSE:
```
Initialize variables
Loop over all words in LEXV
    if we don't have verb:
        word is verb; set verb
        word is direction; set verb=WALK, obj1=direction; exit loop with error
        word is buzz-word; do nothing
        word is object or adjective; print "What should I do with it?"; exit loop with error
        word is unknown; print "I don't know how to do that."; exit loop with error
        word type is unknown; print ""I can't parse that.""; exit loop with error
    if we do have a verb:
        word is buzz-word; do nothing
        word is object or adjective; if ob1 is empty set obj1=word else set obj2=word
        word is direction; if obj1 is not empty print "I can't parse that." else set obj1=direction; exit loop with error
Loop_end
if loop exit was without error and LEXV is not empty:
    verb is not set; print "Huh!" and exit with false
    verb is not WALK and obj1=direction; print "You can't do that!"; exit with false
    verb is walk and obj1 is not direction; print "Go where?"; exit with false
    number of objects is outside definition for this verb;
        if VSTR exits; print VSTR; exit with false
        if VSTR don't exits; print "I don't understand."; exit with false
    exit with true
else exit with false
```
Finally, ```EPARSE``` in [rooms.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/rooms.zil):
 ```   
;"Try to parse the LEX-buffer and fill PRSVEC with verb, direct object and
  indirect object. If successfull TRUE is returned; otherwise FALSE.
    SV      The lexbuffer to parse (not really a vector but name remains
            for historcal purpose.
    SILENT? If TRUE no messages will be printed (for echo)."
<ROUTINE EPARSE (SV SILENT?
                "AUX" (VEC ,PRSVEC) (PRS T) (CNT <GETB .SV 1>) (VERB? <>)
                       W ARG-MIN ARG-MAX (ARG-NUM 0))
    ;"Clear PRSVEC"
    <PUT .VEC 1 <>>
    <PUT .VEC 2 <>>
    <PUT .VEC 3 <>>
    <COND (<0? .CNT>)                               ;"Empty input; return PRS (T)"
          (<DO (I 1 .CNT)                           ;"Traverse over all words in input."
            <SET W <GET .SV <+ <* <- .I 1> 2> 1>>>  ;"Current word"
            <COND (<NOT .VERB?>                     ;"VERB? not set; Search for VERB or DIRECTION, ignore BUZZ."
                <COND (<WT? .W PS?VERB>
                        <SET VERB? .W>
                        <SET ARG-MAX <VMAX .VERB?>>
                        <SET ARG-MIN <VARGS .VERB?>>
                        <PUT .VEC 1 .VERB?>)
                      (<OR <WT? .W PS?DIRECTION> <=? .W ,W?LEAVE>>
                        <COND (<=? .W ,W?LEAVE> <SET .W ,W?OUT>)>
                        <SET VERB? ,W?WALK>
                        <SET ARG-MAX 1>
                        <SET ARG-MIN 1>
                        <SET ARG-NUM 1>
                        <PUT .VEC 1 .VERB?>
                        <PUT .VEC 2 .W>
                        <SET PRS T> <SET .I .CNT>)
                      (<WT? .W PS?BUZZ-WORD>)
                      (<OR <WT? .W PS?OBJECT> <WT? .W PS?ADJECTIVE> <0? .W>>
                        <COND (<OR <WT? .W PS?OBJECT> <WT? .W PS?ADJECTIVE>>
                                <OR .SILENT?
                                <TELL "What should I do with it?" CR>>)     ;"VERB? is not set but found an OBJECT."
                             (<OR .SILENT?
                                <TELL "I don't know how to do that." CR>>)> ;"VERB? is not set and found an unknown word."
                        <SET PRS <>> <SET .I .CNT>)                   
                      (<OR .SILENT? <TELL "I can't parse that." CR>>        ;"Illegal PoS. Shouldn't happen."
                        <SET PRS <>> <SET .I .CNT>)>)
                  (<NOT <0? .W>>                                            ;"VERB is found and legal word"
                <COND (<WT? .W PS?BUZZ-WORD>)                   ;"Ignore BUZZ"
                      (<OR <WT? .W PS?OBJECT> <WT? .W PS?ADJECTIVE>>
                        <SET ARG-NUM <+ .ARG-NUM 1>>
                        <COND (<2 .VEC> <PUT .VEC 3 .W>)
                              (T <PUT .VEC 2 .W>)>)
                      (<WT? .W PS?DIRECTION>
                        <COND (<2 .VEC>
                                <OR .SILENT? <TELL "I can't parse that." CR>>   ;"GO KNIFE SOUTH"
                                <SET PRS <>> <SET .I .CNT>)
                              (<PUT .VEC 2 .W>
                                <SET ARG-NUM <+ .ARG-NUM 1>>
                                <SET PRS T> <SET .I .CNT>)>)>)
                  (T <OR .SILENT? <TELL "I don't know that word." CR>>              ;"Unknown word"
                <SET PRS <>> <SET .I .CNT>)>>)>
    <COND (<AND .PRS <NOT <0? .CNT>>>
            <COND (<NOT .VERB?> <OR .SILENT? <TELL "Huh?" CR>> <SET PRS <>>);"No VERB?"
                  (<AND <L=? .ARG-NUM .ARG-MAX> <G=? .ARG-NUM .ARG-MIN>>    ;"Verb present and arguments in allowed range."
                <COND (<VACTION? .VERB?>
                    <COND (<WT? <2 .VEC> PS?DIRECTION>
                        <OR .SILENT? <TELL "You can't do that!" CR>>        ;"If VACTION? = T (Verb is not WALK) and PRSO is DIRECTION. GET SOUTH."
                        <SET PRS <>>)>)
                      (T
                    <COND (<NOT <WT? <2 .VEC> PS?DIRECTION>>
                        <OR .SILENT? <TELL "Go where?" CR>>                 ;"If VACTION? = FALSE (Verb is WALK) and PRSO is not DIRECTION. GO TROLL."
                        <SET PRS <>>)>)>)
                  (T 
                <COND (<NOT <OR .SILENT? <VSTR .VERB?>>>                        ;"Verb present and illegal argument range print VSTR. TIE."
                    <OR .SILENT? <TELL "I don't understand." CR>>)>         ;"Verb present and illegal argument range and empty VSTR. GO."
                <SET PRS <>>)>)>
    .PRS>
```
And as a reminder on how ```COND``` works - Statements like this:
```
    <COND (<WT? .W PS?BUZZ-WORD>)
          (<WT? .W PS?OBJECT> <Do things...>)
          (<WT? .W PS?DIRECTION> <Do things...>)
          (T <Do things ...>)>
```
Means that if ```.W``` is a buzz-word, because this ```COND``` is missing a ```<Do things...>```-part, nothing is done but no more conditionals are tested.  
  
In ZIL the ```PRSVEC``` is defined as a table with four WORD (16 bits) slots. The first slot is unused to retain the same indexes as in MDL. 
If we use the same sentence as above, ```"throw the knife at the troll"```, ```PRSVEC``` will contain:
```
Byte 0 1 2-3     4-5     6-7
     0 0 W?THROW W?KNIFE W?TROLL
```
  
Next: Part 5 - Walking around
