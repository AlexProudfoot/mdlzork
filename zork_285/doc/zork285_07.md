# Recreating Zork 285 in ZIL - Part 7 - Matching the syntax
This is part 7 in an ongoing series. The previous part: "Part 6 - The object table and how the player moves around" is [here](https://github.com/heasm66/mdlzork/blob/master/zork_285/doc/zork285_06.md).  
  
In this part we will examine how to access the syntax for the verbs in ZIL. In MDL you might remember that each verb was a vector that in slot 2 had the name, VFCN, of the function that handles this verb. In MDL we simply extract this name and call the function. In ZIL it is a little bit more complicatet.
## The syntax tables
Wnen ZIL encounters a ```SYNTAX``` instruction it stores the verb and the prepositions in the vocabulary and populates the ```VERBS```, ```ACTIONS``` and ```PREACTIONS``` tables. The ```VERBS``` table is a table of word pointers to a syntax table for that verb. Verb with verb number 255 is in the first slot and verb with verb number 254 in the second slot and so on. The syntax tables starts with a byte that tells how many syntaxes there is for this verb then follows each syntax occupying 8 bytes each. The 8 bytes contains, in this order; number of objects, preposition word number 1, preposition word number 2, findbit for object 1, findbit for object 2, search options for object 1, search options for object 2 and action number. The action number is the index in the ```ACTIONS``` table and the ```PREACTIONS``` table that contains the routine address for the action and the preaction, respectively.   
  
To illustrate; if we compile and run this program:
```
<VERSION XZIP>          ;"Version 5"
<CONSTANT RELEASEID 1>

;"Examples from Zork 1"
<SYNTAX TAKE OBJECT (FIND TAKEBIT) (ON-GROUND IN-ROOM MANY) = V-TAKE PRE-TAKE>
<SYNONYM TAKE GET HOLD CARRY REMOVE GRAB CATCH>

<SYNTAX GIVE OBJECT (MANY HELD HAVE) TO OBJECT (FIND ACTORBIT) (ON-GROUND) = V-GIVE PRE-GIVE>
<SYNTAX GIVE OBJECT (FIND ACTORBIT) (ON-GROUND) OBJECT (MANY HELD HAVE) = V-SGIVE PRE-SGIVE>
<SYNONYM GIVE DONATE OFFER FEED>

;"KILL and ATTACK are seperated to overcome limit in z3 on number of synonyms."
<SYNTAX KILL OBJECT (FIND ACTORBIT) (ON-GROUND IN-ROOM) WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE) = V-ATTACK>
<SYNONYM KILL MURDER SLAY DISPATCH>

<SYNTAX ATTACK OBJECT (FIND ACTORBIT) (ON-GROUND IN-ROOM) WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE) = V-ATTACK>
<SYNONYM ATTACK FIGHT HURT INJURE HIT>

<ROUTINE V-TAKE () <>>
<ROUTINE PRE-TAKE () <>>
<ROUTINE V-GIVE () <>>
<ROUTINE PRE-GIVE () <>>
<ROUTINE V-SGIVE () <>>
<ROUTINE PRE-SGIVE () <>>
<ROUTINE V-ATTACK () <>>

<ROUTINE GO () 
    <CRLF>
    <TELL "Verb# W?TAKE=" N <GETB ,W?TAKE 7> CR>
    <TELL "Verb# W?GIVE=" N <GETB ,W?GIVE 7> CR>
    <TELL "Verb# W?KILL=" N <GETB ,W?KILL 7> CR>
    <TELL "Verb# W?ATTACK=" N <GETB ,W?ATTACK 7> CR>
    <TELL "VERBS[0]=" N <0 ,VERBS> CR>
    <TELL "VERBS[1]=" N <1 ,VERBS> CR>
    <TELL "VERBS[2]=" N <2 ,VERBS> CR>
    <TELL "VERBS[3]=" N <3 ,VERBS> CR>
    <TELL "ACTIONS[0]=" N <0 ,ACTIONS> CR>
    <TELL "ACTIONS[1]=" N <1 ,ACTIONS> CR>
    <TELL "ACTIONS[2]=" N <2 ,ACTIONS> CR>
    <TELL "ACTIONS[3]=" N <3 ,ACTIONS> CR>
    <TELL "PREACTIONS[0]=" N <0 ,PREACTIONS> CR>
    <TELL "PREACTIONS[1]=" N <1 ,PREACTIONS> CR>
    <TELL "PREACTIONS[2]=" N <2 ,PREACTIONS> CR>
    <TELL "PREACTIONS[3]=" N <3 ,PREACTIONS> CR>
>
```
We get this output:
```
Verb# W?TAKE=255
Verb# W?GIVE=254
Verb# W?KILL=253
Verb# W?ATTACK=252
VERBS[0]=673
VERBS[1]=682
VERBS[2]=699
VERBS[3]=708
ACTIONS[0]=180
ACTIONS[1]=182
ACTIONS[2]=184
ACTIONS[3]=186
PREACTIONS[0]=181
PREACTIONS[1]=183
PREACTIONS[2]=185
PREACTIONS[3]=0
```
We see that the ```VERBS``` table contains ```$02A1 $02AA $02BB $02C4``` and if we look at these addresses:
```
       # NO P1 P2 F1 F2 O1 O2 ACT
02A1: 01 01 00 00 2F 00 34 F0 00    <SYNTAX TAKE OBJECT (FIND TAKEBIT) (ON-GROUND IN-ROOM MANY) = V-TAKE PRE-TAKE>
02AA: 02 02 00 00 2E 00 10 86 02    <SYNTAX GIVE OBJECT (FIND ACTORBIT) (ON-GROUND) OBJECT (MANY HELD HAVE) = V-SGIVE PRE-SGIVE>
         02 00 FF 00 2E 86 10 01    <SYNTAX GIVE OBJECT (MANY HELD HAVE) TO OBJECT (FIND ACTORBIT) (ON-GROUND) = V-GIVE PRE-GIVE>
02BB: 01 02 00 FE 2E 2D 30 C2 03    <SYNTAX KILL OBJECT (FIND ACTORBIT) (ON-GROUND IN-ROOM) WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE) = V-ATTACK>
02C4: 01 02 00 FE 2E 2D 30 C2 03    <SYNTAX ATTACK OBJECT (FIND ACTORBIT) (ON-GROUND IN-ROOM) WITH OBJECT (FIND WEAPONBIT) (HELD CARRIED HAVE) = V-ATTACK>

#               Number of SYNTAX lines for this verb
NO  NOBJ    0   Number of OBJECT
P1  PREP1   1   First preposition word number
P2  PREP2   2   Second preposition word number
F1  FIND1   3   1st OBJECTs FIND (which attribute flag) 
F2  FIND2   4   2nd OBJECTs FIND (which attribute flag)
O1  OPTS1   5   1st OBJECTs search oprions, see below
O2  OPTS2   6   2st OBJECTs search options, see below
ACT ACTION  7   Points to row in ,ACTIONS/,PREACTIONS with right action/preaction-routine

Search options:
Bit 0
    1   HAVE
    2   MANY
    3   TAKE
    4   ON-GROUND
    5   IN-ROOM
    6   CARRIED
    7   HELD
```
The verb ```GIVE``` has the verb number = 254 and if we look at slot 1 (calculated as 255-254) in ```VERBS``` we find that the syntax table for ```GIVE``` is at ```$02AA```. The first byte at this address is 2, meanin that there are two different syntaxes for this verb. The first syntax, ```02 00 00 2E 00 10 86 02```, tells that there are two objects, no prepositions, the first objects ```FIND``` are attribute flag 46, there is no ```FIND``` on the second object, options for first object has bit 4 set (```ON-GROUND```), options for the second object have bits 1, 2 and 7 set (```HAVE MANY HELD```) and the action and preaction routine are in slot 2 of ```ACTIONS``` and ```PREACTIONS```, respectively. The second syntax, ```02 00 FF 00 2E 86 10 01```, tells that there are two objects, no preposition for first object, preposition with number 255 (```W?TO```) for second object, there is no ```FIND``` on the first object, the second objects ```FIND``` is attribute flag 46, options for first object have bits 1, 2 and 7 set (```HAVE MANY HELD```), options for the second object has bit 4 set (```ON-GROUND```) and the action and preaction routine are in slot 1 of ```ACTIONS``` and ```PREACTIONS```, respectively.  
  
The matching between the players input to the right syntax and the support for ```FIND``` and the searcher options are all up to the parser library to supply.
## Converting ```VFCN``` to ZIL and call the corresponding verb action
In MDL ```VFCN``` is simply a constant that points to the slot where the verb functions name are stored in the verb vector. In ZIL ```VFCN``` instead is a function that extracts the function from the syntax table. *Zork 285* only use the actions and donÂ´t use any prepositions, finds, search options or preactions. Every syntax in *Zork 285* is in the form ```<SYNTAX TAKE = TAKE>``` and there is only one syntax per verb. This simplifies the syntax matching quite a bit for ```VFCN``` ([defs.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/defs.zil)):
```
<ROUTINE VFCN (V "AUX" SYNTAX-PTR)
    <COND (<WT? .V PS?VERB>
        <SET SYNTAX-PTR <GET ,VERBS <- 255 <GETB .V 7>>>>
        <SET SYNTAX-PTR <+ .SYNTAX-PTR 1>>
        <RETURN <GET ,ACTIONS <GETB .SYNTAX-PTR 7>>>)>
    <RETURN 0>>
```
This means that the instruction ```<APPLY <VFCN <1 ,PRSVEC>>>``` in ```RDCOM``` ([rooms.zil](https://github.com/heasm66/mdlzork/blob/master/zork_285/rooms.zil)) will call the verb function for the verb in first slot in ```PRSVEC```, if there is any.  
  
Next: Part 8 - Handling the inventory
